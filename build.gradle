buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
		jcenter()
		maven { url 'https://plugins.gradle.org/m2/' }
	}
	dependencies {
		classpath("de.carne:java-gradleplugins:0.4.0")
		classpath("gradle.plugin.install4j.install4j:gradle_plugin:7.0.4")
	}
}

apply plugin: 'java'
apply plugin: 'de.carne.gradleplugins.java'
apply plugin: 'jacoco'
apply plugin: 'distribution'
apply plugin: 'com.install4j.gradle'
apply plugin: 'eclipse'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
	mavenLocal()
	mavenCentral()
	jcenter()
	if(project.version.endsWith("-SNAPSHOT")) {
		maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
		maven { url "https://oss.jfrog.org/libs-snapshot/" }
	} 
}

dependencies {
	compile("de.carne.common:java-boot:2.1.+")
	compile("de.carne.common:java-default:8.3.+")
	compile("de.carne:java-jfx:8.1.+")
	compile("org.bouncycastle:bcpkix-jdk15on:1.59")
	testCompile("junit:junit:4.12")
}

jacocoTestReport {
	reports {
		xml.enabled true
		xml.destination file("${buildDir}/reports/jacoco/coverage.xml")
		html.enabled true
		html.destination file("${buildDir}/reports/jacoco")
		csv.enabled false
	}
}

import java.text.SimpleDateFormat

jar {
	def buildTimestamp = new SimpleDateFormat("yyyyMMddHHmmss").format(new Date());
	
	manifest {
		attributes(
			'Main-Class': 'de.carne.ApplicationLoader',
			'X-Version-ProjectId': project.name,
			'X-Version-ProjectName' : project.name,
			'X-Version-ProjectVersion': version,
			'X-Version-ProjectBuild' : buildTimestamp
		)
	}
	from configurations.runtime.collect {
		it.name.matches('(java-default-.*.jar)|(java-jfx-.*.jar)') ? zipTree(it).matching { exclude 'META-INF/**' } : it
	}
}

distributions {
	main {
		contents {
			from jar
		}
	}
}

tasks.withType(Tar){
	compression = Compression.GZIP
	extension = 'tar.gz'
}

install4j {
	installDir = file(project.hasProperty("install4jHome") ? install4jHome : ".")
}

task assembleMedia(type: com.install4j.gradle.Install4jTask) {
	projectFile = file("src/main/install4j/certmgr.install4j")
	variables = [
		i4jProjectName: project.name,
		i4jProjectVersion: version,
		i4jProjectBaseDir: projectDir,
		i4jProjectBuildDir: buildDir
	]
}

assembleDist.finalizedBy(assembleMedia)

import org.gradle.plugins.ide.eclipse.model.AccessRule

eclipse {
	classpath {
		downloadSources = true
		downloadJavadoc = true
		file {
			whenMerged {
				def jre = entries.find { it.path.contains 'org.eclipse.jdt.launching.JRE_CONTAINER' }
				
				jre.accessRules.add(new AccessRule('0', 'javafx/**'))
			}
		}
	}
}
